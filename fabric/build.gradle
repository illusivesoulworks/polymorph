plugins {
    id 'fabric-loom' version '0.7-SNAPSHOT'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
}

repositories {
    maven { 
        url = "https://jitpack.io" 
    }
    maven {
        url = "https://maven.shedaniel.me"
    }
    maven {
        name = "Ladysnake Mods"
        url = "https://ladysnake.jfrog.io/artifactory/mods"
    }
    maven {
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Modmuss50"
        url = "https://maven.modmuss50.me/"
    }
    maven {
        name = "Modmaven"
        url = "https://modmaven.dev/"
        content {
            includeGroup "appeng"
        }
    }
    maven {
        name = "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        name = "Technici4n"
        url = "https://raw.githubusercontent.com/Technici4n/Technici4n-maven/master/"
        content {
            includeGroup "net.fabricmc.fabric-api" // until PR # is merged
            includeGroup "dev.technici4n"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation fabricApi.module("fabric-api-base", project.fabric_version)
    modImplementation fabricApi.module("fabric-lifecycle-events-v1", project.fabric_version)
    modImplementation fabricApi.module("fabric-networking-api-v1", project.fabric_version)
    modImplementation fabricApi.module("fabric-resource-loader-v0", project.fabric_version)
    modImplementation fabricApi.module("fabric-command-api-v1", project.fabric_version)
    modImplementation fabricApi.module("fabric-commands-v0", project.fabric_version)
    modImplementation fabricApi.module("fabric-networking-v0", project.fabric_version)

    modApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cca_version}"
    modApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-block:${project.cca_version}"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems:${project.rei_version}"
    modRuntime "me.shedaniel:RoughlyEnoughItems:${project.rei_version}"
    modCompileOnly "curse.maven:fabricfurnaces-315534:${project.file_fabricfurnaces}"
    modRuntime "curse.maven:fabricfurnaces-315534:${project.file_fabricfurnaces}"
    modRuntime "me.sargunvohra.mcmods:autoconfig1u:3.3.1"
    modCompileOnly "curse.maven:ironfurnaces-318036:${project.file_ironfurnaces}"
    modRuntime "curse.maven:ironfurnaces-318036:${project.file_ironfurnaces}"
    modRuntime "teamreborn:energy:0.1.1"
    modCompileOnly "curse.maven:improvedstations-355914:3032328"
    modRuntime "curse.maven:improvedstations-355914:3032328"
    modCompileOnly "appeng:appliedenergistics2-fabric:8.4.0-alpha.2"
    modRuntime "appeng:appliedenergistics2-fabric:8.4.0-alpha.2"
    modCompileOnly "curse.maven:fastbench-364531:${project.file_fastbench}"
    modRuntime "curse.maven:fastbench-364531:${project.file_fastbench}"
    modRuntime "curse.maven:fastfurnace-364540:${project.file_fastfurnace}"
    modRuntime "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cca_version}"
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-block:${project.cca_version}"
}

processResources {
    from("CHANGELOG.md")
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

sourceSets {
    main {
        resources {
            srcDirs += '../docs'
        }
    }
}

curseforge {
    project {
        apiKey = findProperty('curseKey') ?: 0
        id = "${curse_id}"
        releaseType = "${curse_release}"
        changelogType = 'markdown'
        changelog = file('CHANGELOG.md')
        addGameVersion "Java 8"
        addGameVersion "Fabric"
        "${curse_versions}".split(',').each {
            addGameVersion "${it}"
        }
        addArtifact(sourcesJar)
        mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))

        relations {
            embeddedLibrary 'fabric-api'
            embeddedLibrary 'cardinal-components'
        }

        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
    }
    options {
        forgeGradleIntegration = false
    }
}
